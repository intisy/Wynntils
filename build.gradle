plugins {
    id "architectury-plugin" version "${architectury_plugin_version}"
    id "dev.architectury.loom" version "${architectury_loom_version}" apply false
    id "com.diffplug.spotless" version "${spotless_version}"
}

architectury {
    minecraft = minecraft_version
}

// Add "wynntils.hotswap=true" to your personal gradle properties file to use hotswap.
// By default, this is C:\Users\<your username>\.gradle\gradle.properties on Windows
// or ~/.gradle/gradle.properties on Linux/MacOS.
def usingHotswapAgent = project.hasProperty("wynntils.hotswap") ? project.getProperty("wynntils.hotswap") == "true" : false
def clientCount = 4

// On the release branches, this string will be automatically updated by the bots
// Do not change this in the main branch!
version = "3.0.0-SNAPSHOT"

subprojects {
    apply plugin: "dev.architectury.loom"

    repositories {
        maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
        maven { url = "https://jitpack.io" }
    }

    loom {
        silentMojangMappingsLicense()
        accessWidenerPath = file("src/main/resources/wynntils.accessWidener")

        runs {
            client {
                property("devauth.configDir", getRootProject().file(".devauth").absolutePath)
                if (usingHotswapAgent) {
                    vmArgs "-XX:+AllowEnhancedClassRedefinition"
                    vmArgs "-XX:+ClassUnloading"
                    vmArgs "-XX:HotswapAgent=fatjar"
                }
                vmArgs "-ea"
                client()
            }

            for (int i = 1; i <= clientCount; i++) {
                create("client${i}") {
                    property("devauth.configDir", project.rootProject.file(".devauth").absolutePath)

                    if (usingHotswapAgent) {
                        vmArgs "-XX:+AllowEnhancedClassRedefinition"
                        vmArgs "-XX:+ClassUnloading"
                        vmArgs "-XX:HotswapAgent=fatjar"
                    }

                    vmArgs "-ea"
                    client()
                }
            }
        }
    }

    tasks.register("runAllClients") {
        group = "loom"
        description = "Runs all client configurations in parallel"

        doLast {
            def projectDir = project.rootDir
            def projectName = project.name
            def count = clientCount
            def javaHome = System.getProperty("java.home")
            println "Starting ${count} clients in parallel using JAVA_HOME=${javaHome}..."
            def processes = []
            for (int i = 1; i <= count; i++) {
                println "Launching :${projectName}:runClient${i}"
                def pb = new ProcessBuilder('./gradlew', ":${projectName}:runClient${i}")
                pb.directory(projectDir)
                pb.environment().put("JAVA_HOME", javaHome)
                pb.inheritIO()
                def process = pb.start()
                processes << [process: process, num: i]
            }
            println "All clients launched, waiting for them to exit..."
            processes.each { 
                it.process.waitFor()
                println "Client ${it.num} exited with code: ${it.process.exitValue()}"
            }
            println "All clients exited."
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${parchment_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "com.diffplug.spotless"

    base {
        archivesName = archives_base_name
    }
    version = rootProject.version

    repositories {
        maven { url "https://maven.parchmentmc.org/" }
        maven { url "https://jitpack.io" }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(java_version))
        }
        withSourcesJar()
    }

    spotless {
        java {
            // define the steps to apply to Java source code
            importOrder()
            removeUnusedImports()
            palantirJavaFormat(spotless_palantir_version)
            trimTrailingWhitespace()
            endWithNewline()
            ratchetFrom('origin/main')
            // Custom rule from https://github.com/apache/geode
            custom "Refuse wildcard imports", {
                if (it =~ /\nimport .*\*;/) {
                    throw new AssertionError("Do not use wildcard imports. 'spotlessApply' cannot resolve this issue.")
                }
            }
            custom "Refuse IntelliJ annotations", {
                if (it =~ /\nimport org\.jetbrains\.annotations\./) {
                    throw new AssertionError("Do not use IntelliJ annotations. 'spotlessApply' cannot resolve this issue.")
                }
            }
            custom "No empty line after opening curly brace", {
                it.replaceAll(/\{\n\n/, '{\n')
            }
            licenseHeader("/*\n" +
                    " * Copyright Â© Wynntils \$YEAR.\n" +
                    " * This file is released under LGPLv3. See LICENSE for full license details.\n" +
                    " */")
                    .updateYearWithLatest(true)
        }
        json {
            target "src/**/*.json"
            /*
             1. spotless replaces the Unicode escapes with the actual characters, worsening readability.
             2. schemaVersion must be at the top or else Fabric will complain
             */
            targetExclude(
                    "src/main/resources/assets/wynntils/font/*.json",
                    "src/main/resources/fabric.mod.json")
            gson()
                    .indentWithSpaces(2)
                    .sortByKeys()
                    .version(spotless_gson_version)
            trimTrailingWhitespace()
            endWithNewline()
        }
        format "lang", {
            target "src/main/resources/assets/wynntils/lang/*.json"
            custom "No empty language json files", {
                it.replaceAll(/^\{\}\n$/, '')
            }
        }
        groovyGradle {
            target '**/*.gradle'
            greclipse("${spotless_greclipse_version}").configFile("${rootDir}/greclipse.properties")
            trimTrailingWhitespace()
            endWithNewline()
        }

        format "misc", {
            // define the files to apply `misc` to
            target "*.gradle", "*.md", ".gitignore", "*.properties"
            targetExclude("CHANGELOG.md")

            // define the steps to apply to those files
            trimTrailingWhitespace()
            leadingTabsToSpaces()
            endWithNewline()
        }
    }
}
